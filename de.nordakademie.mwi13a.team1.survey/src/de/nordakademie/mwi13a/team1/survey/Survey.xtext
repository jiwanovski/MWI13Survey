grammar de.nordakademie.mwi13a.team1.survey.Survey with org.eclipse.xtext.common.Terminals

generate survey "http://www.nordakademie.de/mwi13a/team1/survey/Survey"

/*Root element Survey*/
Survey returns Survey:
	(questionnaire+=Questionnaire)+
;

/*A Questionnaire must contain at least 1 Part*/
Questionnaire:
	'Questionnaire:' name=STRING '{'
		'Questionnaire-ID:' id=Identifier
		(part+=Part)+
	'}'
;

/*A Part must contain at least 1 Question*/
Part:
	'Part:' name=STRING '{'
		'Part-ID:' id=Identifier
		(question+=Question)+
	'}'
;

/*For each Question will be defined a type and wether it's a mandatory field or not*/ 
Question:
	'Question:' name=STRING '{'
		'Question-ID:' id=Identifier
		mandatory?='Mandatory Field'?
		'Type:' questionType=SurveyTerminalTypes //(dropdown+=DropDown | combobox=ComboBoxT | radio+=Radio | matrix+=Matrix | textln+=TextLn | textbl+=TextBl)

		/*For each Question can be defined an arbitraty number of Answers. 
		 *The Question Types TextLn and TextBl don't require any answers because there's an STRING expected by default.*/
		
	'}'
;

MatrixQuestion:
	'Question-ID:' id=Identifier
	'Question:' name=STRING
;

Answer:
	'Answer-ID:' id=Identifier
	'Answer:' name=STRING	
;

SurveyTerminalTypes:
	{TextLine} name='TextLine' '(' length=INT ')' |
	{TextBlock} name='TextBlock' '(' length=INT ')' |
	{Matrix} name='Matrix' '{'
		'Scale:' '(' 
			answer+=Answer ('|' 
			answer+=Answer)* 
			')'
		'Questions:' '(' 
			matrixQuestion+=MatrixQuestion ('|' 
			matrixQuestion+=MatrixQuestion)* 
		')'
	'}' |
	{ComboBox} name='ComboBox' '{'
		(answer+=Answer)+ 
	'}'|
	{DropDown} name='DropDown' '{'
		(answer+=Answer)+ 
	'}'|
	{Radio} name='Radio' '{'
		(answer+=Answer)+
	'}'
;
/*Following Question Types are available:
*TextLn (Textline) and TextBl (Textblock) require the indication of the maximal length of text input in brackets.*/


Identifier:
	ID | STRING | INT
;

